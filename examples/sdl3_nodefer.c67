import sdl3 as sdl

cstruct SDL_Event {
    type as uint32,
    timestamp as uint64
}

width = 620
height = 387

sdl.SDL_Init(sdl.SDL_INIT_VIDEO) or! {
    exitf("SDL_Init failed\n")
}

window = sdl.SDL_CreateWindow("Hello World!", width, height, 0) or! {
    exitf("Failed to create window\n")
}

renderer = sdl.SDL_CreateRenderer(window, 0) or! {
    exitf("Failed to create renderer\n")
}

file = sdl.SDL_IOFromFile("img/grumpy-cat.bmp", "rb") or! {
    exitf("Error reading file\n")
}

bmp = sdl.SDL_LoadBMP_IO(file, 1) or! {
    exitf("Error creating surface\n")
}

tex = sdl.SDL_CreateTextureFromSurface(renderer, bmp) or! {
    exitf("Error creating texture\n")
}

printf("Starting main loop...\n")

running := 1
event := c.malloc(192) as SDL_Event

@ running > 0 max inf {
    has_event := sdl.SDL_PollEvent(event)
    
    has_event {
        0 => {}
        ~> {
            event.type {
                256 => { running = 0 }
                768 => {
                    scancode := peek32(event, 16)
                    scancode {
                        41 => { running = 0 }
                    }
                }
            }
        }
    }

    sdl.SDL_RenderClear(renderer)
    sdl.SDL_RenderTexture(renderer, tex, 0, 0)
    sdl.SDL_RenderPresent(renderer)
    sdl.SDL_Delay(16)
}

printf("Cleaning up...\n")
c.free(event)
sdl.SDL_DestroyTexture(tex)
sdl.SDL_DestroySurface(bmp)
sdl.SDL_DestroyRenderer(renderer)
sdl.SDL_DestroyWindow(window)
sdl.SDL_Quit()
printf("Done!\n")
